Start = DecList

DecList = Dec+
Dec = VarDec::dec / FuncDec::dec / TemplateDec::dec / ClassDec::dec / JsImportDec::dec / JsExportDec::dec / OperDec::dec

VarDecNoInit = Type ":" Var ";"
VarDecInit = Type ":" Var "=" Exp ";"
VarDec = VarDecNoInit::vardec / VarDecInit::vardec
FuncDec = "Funktion" ":" ID "(" FieldList::params ")" "=>" "(" FieldList::result ")" "{" StmList "}"
TemplateDec = "template" "<" IDList ">" ":" Dec
ClassDec = "class" ":" ID "{" ClassMemberList "}" / "class" ":" ID "<-" IDList "{" ClassMemberList "}"
JsImportDec = "function" ":" ID "(" FieldList::params ")" "=>" "(" FieldList::result ")" "=" "jsload" "(" String::module "," String::name ")" ";"
JsExportDec = "jsexport" "(" ID "," String ")" ";"
OperDec = "operator" ":" ID "(" FieldList::params ")" "=>" "(" FieldList::result ")" "{" StmList "}"

ClassMemberList = (PublicMembers::members / PrivateMembers::members)+
PublicMembers = "pub" ":" DecList / DecList
PrivateMembers = "priv" ":" DecList

FieldList = (Field)* ("," Field)*
Field = Type ":" Var

ExpList = (Exp::exp)* ("," Exp::exp)*
Exp = BinOpExp::exp
BinOpExp = CompOpExp::exp ((AndOp::op / OrOp::op) CompOpExp::exp)*
CompOpExp = SumExp::exp ((EqOp::op / NeOp::op / LtOp::op / LeOp::op / GtOp::op / GeOp::op) SumExp::exp)*
SumExp = ProdExp::exp ((AddOp::op / SubOp::op) ProdExp::exp)*
ProdExp = UnaryOpExp::exp ((MulOp::op / DivOp::op) UnaryOpExp::exp)*
UnaryOpExp = (SubOp::op / DerefOp::op / AddressOp::op / NotOp::op)* PrimaryExp::exp
PrimaryExp = ConstantExp::exp / VarExp::exp / ParenExp::exp / ArrayExp::exp / FuncExp::exp / SizeOfExp::exp
ConstantExp = RealExp::exp / IntExp::exp / StringExp::exp / BoolExp::exp

ArrayExp = "[" ExpList "]"
ParenExp = "(" Exp::exp ")"
SizeOfExp = "Größevon" "(" Exp::exp ")"
FuncExp = "(" FieldList::params ")" "=>" "(" FieldList::result ")" Stm
VarExp = Var

AndOp = "&&"
OrOp = "||"
EqOp = "=="
NeOp = "!="
LtOp = "<"
LeOp = "<="
GtOp = ">"
GeOp = ">="
AddOp = "+"
SubOp = "-"
MulOp = "*"
DivOp = "/"
DerefOp = "*"
AddressOp = "&"
NotOp = "!"

IntExp = Integer
RealExp = Real
StringExp = String
BoolExp = True::bool / False::bool

StmList = Stm+
Stm = DecStm::stm / AssignStm::stm / CompoundStm::stm / IfStm::stm / WhileStm::stm / UntilStm::stm / RepeatStm::stm / BreakStm::stm / ContinueStm::stm / ReturnStm::stm / LoopStm::stm / ExpStm::stm
CompoundStm = "{" StmList "}" / "{" "}"
DecStm = Dec
AssignStm = NormalAssign::stm / AddAssign::stm / SubAssign::stm / MulAssign::stm / DivAssign::stm
IfStm = IfElseList
WhileStm = "während" "(" Exp ")" Stm
UntilStm = "bis" "(" Exp ")" Stm
RepeatStm = "wiederholen" "(" Exp ")" Stm
BreakStm = "ausbruch" ";"
ContinueStm = "weiter" ";"
ReturnStm = "rückkehr" "(" Exp ")" ";" / "rückkehr" ";"
LoopStm = "schleife" Stm
ExpStm = Exp ";"

NormalAssign = Var "=" Exp ";"
AddAssign = Var "+=" Exp ";"
SubAssign = Var "-=" Exp ";"
MulAssign = Var "*=" Exp ";"
DivAssign = Var "/=" Exp ";"

IfElseList = If::ifelse ( ElseIf::ifelse )* Else::ifelse / If::ifelse (ElseIf::ifelse)*
If = "wenn" "(" Exp ")" Stm
ElseIf = "andernfalls" "wenn" "(" Exp ")" Stm
Else = "andernfalls" Stm

Type = PointerType::type / ArrayType::type
PrimaryType = IntType::type / ShortType::type / RealType::type / CharType::type / BoolType::type / NameType::type / ParenType::type / GenericsType::type

PointerType = PrimaryType "*"
ArrayType = PrimaryType ( "[" Exp::exp "]" )*
ParenType = "(" Type ")"
NameType = ID
GenericsType = ID "<" TypeList ">"
IntType = "int" !ID
ShortType = "short" !ID
RealType = "double" !ID
CharType = "char" !ID
BoolType = "bool" !ID

Var = PointerVar::var
ParenVar = "(" Var ")"
SimpleVar = ID
PrimaryVar = SimpleVar::var / ParenVar::var
SuffixVar = PrimaryVar ( CallSuffix::suffix / DotSuffix::suffix / ArrowSuffix::suffix / SubscriptSuffix::suffix )*
PointerVar = {("*")* SuffixVar : pointer}

CallSuffix = "(" ExpList::explist ")"
DotSuffix = "." ID
ArrowSuffix = "->" ID
SubscriptSuffix = "[" Exp::exp "]"

IDList = ID ("," ID)*
ID = { [㐀-龯ぁ-んァ-ヶa-zA-Z_ー] [㐀-龯ぁ-んァ-ヶa-zA-Z0-9０-９_ー]* " "* : id }
Integer = { ( "-" / "" ) [0-9]+ " "* : int }
Real = { [0-9]+ "." [0-9]+ " "* : real }
String = { "\"" ( !"\"" . )* "\"" : string }
True = "wahr"
False = "falsch"
