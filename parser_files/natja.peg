Start = DecList

DecList = Dec+
Dec = FuncDec::dec / VarDec::dec / TemplateDec::dec / ClassDec::dec / JsImportDec::dec / JsExportDec::dec / OperDec::dec

VarDecNoInit = Var "という" Type "をおく" "。"
VarDecInit = Var "という" Type "を" Exp "で初期化" "。"
VarDec = VarDecNoInit::vardec / VarDecInit::vardec
FuncDec = FieldList::params "をとり" FieldList::result "を返す" ID "という関数をおく。" "ここから" StmList "ここまで" / ID "という関数をおく。" "ここから" StmList "ここまで"
TemplateDec = "型" "<" IDList ">" ":" Dec
ClassDec = "クラス" ":" ID "{" ClassMemberList "}" / "クラス" ":" ID "<-" IDList "{" ClassMemberList "}"
JsImportDec = "関数" ":" ID "(" FieldList::params ")" "=>" "(" FieldList::result ")" "=" "js読み込み" "(" String::module "," String::name ")" ";"
JsExportDec = "js書き出し" "(" ID "," String ")" ";"
OperDec = "演算子" ":" ID "(" FieldList::params ")" "=>" "(" FieldList::result ")" "{" StmList "}"

ClassMemberList = (PublicMembers::members / PrivateMembers::members)+
PublicMembers = "公開" ":" DecList / DecList
PrivateMembers = "非公開" ":" DecList

FieldList = (Field)* ("と" Field)*
Field = Type ":" Var

ExpList = (Exp::exp)* ("," Exp::exp)*
Exp = BinOpExp::exp
BinOpExp = CompOpExp::exp ((AndOp::op / OrOp::op) CompOpExp::exp)*
CompOpExp = SumExp::exp ((EqOp::op / NeOp::op / LtOp::op / LeOp::op / GtOp::op / GeOp::op) SumExp::exp)*
SumExp = ProdExp::exp ((AddOp::op / SubOp::op) ProdExp::exp)*
ProdExp = UnaryOpExp::exp ((MulOp::op / DivOp::op) UnaryOpExp::exp)*
UnaryOpExp = (SubOp::op / DerefOp::op / AddressOp::op / NotOp::op)* PrimaryExp::exp
PrimaryExp = ConstantExp::exp / VarExp::exp / ParenExp::exp / ArrayExp::exp / FuncExp::exp / SizeOfExp::exp
ConstantExp = RealExp::exp / IntExp::exp / StringExp::exp / BoolExp::exp

ArrayExp = "[" ExpList "]"
ParenExp = "(" Exp::exp ")"
SizeOfExp = "メモリサイズ" "(" Exp::exp ")"
FuncExp = "(" FieldList::params ")" "=>" "(" FieldList::result ")" Stm
VarExp = Var

AndOp = "かつ"
OrOp = "または"
EqOp = "は"
NeOp = "!="
LtOp = "<"
LeOp = "<="
GtOp = ">"
GeOp = ">="
AddOp = "+"
SubOp = "-"
MulOp = "*"
DivOp = "/"
DerefOp = "*"
AddressOp = "&"
NotOp = "!"

IntExp = Integer
RealExp = Real
StringExp = String
BoolExp = True::bool / False::bool

StmList = Stm+
Stm = DecStm::stm / AssignStm::stm / CompoundStm::stm / IfStm::stm / WhileStm::stm / UntilStm::stm / RepeatStm::stm / BreakStm::stm / ContinueStm::stm / ReturnStm::stm / LoopStm::stm / ExpStm::stm
CompoundStm = "ここから" StmList "ここまで" / "ここから" "ここまで"
DecStm = Dec
AssignStm = NormalAssign::stm / AddAssign::stm / SubAssign::stm / MulAssign::stm / DivAssign::stm
IfStm = IfElseList
WhileStm = Exp "の間" Stm
UntilStm = Exp "まで" Stm
RepeatStm = Exp "回繰り返す" Stm
BreakStm = "抜ける" "。"
ContinueStm = "次へ" "。"
ReturnStm = "終了" "(" Exp ")" "。" / "終了" "(" ")" ";"
LoopStm = "無限ループ" Stm
ExpStm = Exp "。"

NormalAssign = Var "に" Exp "を代入する" "。"
AddAssign = Var "に" Exp "を足す" "。"
SubAssign = Var "から" Exp "を引く" "。"
MulAssign = Var "に" Exp "をかける" "。"
DivAssign = Var "を" Exp "で割る" "。"

IfElseList = If::ifelse ( ElseIf::ifelse )* Else::ifelse / If::ifelse (ElseIf::ifelse)*
If = "もし" Exp "ならば" Stm
ElseIf = "でなければもし" Exp "ならば" Stm
Else = "でなければ" Stm

Type = PointerType::type / ArrayType::type
PrimaryType = IntType::type / ShortType::type / RealType::type / CharType::type / BoolType::type / NameType::type / ParenType::type / GenericsType::type

PointerType = PrimaryType "*"
ArrayType = PrimaryType ( "[" Exp::exp "]" )*
ParenType = "(" Type ")"
NameType = ID
GenericsType = ID "<" TypeList ">"
IntType = "整数" !ID
ShortType = "整数32" !ID
RealType = "実数" !ID
CharType = "文字" !ID
BoolType = "真偽" !ID

Var = PointerVar::var
ParenVar = "(" Var ")"
SimpleVar = ID
PrimaryVar = SimpleVar::var / ParenVar::var
SuffixVar = PrimaryVar ( CallSuffix::suffix / DotSuffix::suffix / ArrowSuffix::suffix / SubscriptSuffix::suffix )*
PointerVar = {("*")* SuffixVar : pointer}

CallSuffix = "(" ExpList::explist ")"
DotSuffix = "の" ID
ArrowSuffix = "->" ID
SubscriptSuffix = "の" Exp::exp "番目"

IDList = ID ("," ID)*
ID = { [㐀-龯ァ-ヶa-zA-Z_ー] [㐀-龯ァ-ヶa-zA-Z0-9０-９_ー]* " "* : id }
Integer = { ( "-" / "" ) [0-9]+ " "* : int }
Real = { [0-9]+ "." [0-9]+ : real }
String = { "\"" ( !"\"" . )* "\"" : string }
True = "真"
False = "偽"
